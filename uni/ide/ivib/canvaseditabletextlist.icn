#
# $Id: canvaseditabletextlist.icn,v 1.2 2006-07-09 23:43:12 rparlett Exp $
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a EditableTextList on the canvas.
#
class CanvasEditableTextList : CanvasComponent : EditableTextList()
   method min_height()
      # need a smart calculation font height + borders + scrollbar
      # for now based on constants in gui.icn, guiconst.icn we go with
      # 14 + 8*2 + 22
      return 52
   end
 
   method min_width()
      # need a smart calculation font height + borders + scrollbar
      # for now based on constants in gui.icn, guiconst.icn we go with
      # 14 + 5*2 + 22
      return 46
   end

   #
   # Generate code into Code object c.
   #
   method gen_code(c)
      self.CanvasComponent.gen_code(c)
      c.line(name || ".set_contents([" || list2string(self.contents) || "])")
   end

   #
   # Copy fields from self to c.
   #
   method cp_fields(c)
      c.set_contents(copy(self.contents))
   end

   #
   # Copy object.
   #
   method cp()
      local c
      c := CanvasEditableTextList()
      self.CanvasComponent.cp(c)
      self.cp_fields(c)
      return c
   end

   #
   # Duplicate object.
   #
   method dup(pc)
      local c
      c := CanvasEditableTextList()
      self.CanvasComponent.dup(c, pc)
      self.cp_fields(c)
      return c
   end

   #
   # Return the I/O template.
   #
   method get_template()
      return self.CanvasComponent.get_template() ||| 
         [["Contents", "contents"]
         ]
   end

   #
   # Open the configuration dialog box.
   #
   method dialog()
      return CanvasEditableTextListDialog(self)
   end

   initially
      self.CanvasComponent.initially()
      self.EditableTextList.initially()
      self.class_name := "EditableTextList"
      self.import_name := "gui"
      self.name := "editable_text_list"
end

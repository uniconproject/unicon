#  
#  srcfile.icn
#  Handles the collected Source Files from the Icode
#  Author : Ziad Al-Sharif
#  Date   : Feb 24, 2008
# 

$include "defaults.icn"
link bincvt
link strings

$define CHARACTER &letters++&digits

#
# It is used to index the loaded source files in the srcText list
# 
record fileIndex(
   fbegin, # where the source file index starts in srcText
   fend    # where the source file index ends in srcText
   )

#
# Contains all of the source files issues such as found files and so  
#
class SourceFile(
   procMainFile, # The Source File that has the procedure main
   procMainLine, # The Source Line that has the procedure main
   exeName,      # The executable file name
   exeSize,      # The executable file size

   userFiles,    # a set of the loaded user files
   libFiles,     # a set of the loaded library files

   binPath,      # the path of the VM: iconx or wiconx.exe
   srcPath,      # String of paths that will be searched for source files
   libPath,      # String of paths that will be searched for library files

   fileNames,    # List with all source file names that are in the binary
   srcText,      # List holds all source files text
   srcIndex,     # Table of our position in the source file; fname is the key
   foundFiles,   # Set with all file names that were found - loaded
   missingFiles  # Set with all file names that were not found - not loaded 
   )

#
# Extracts the file name from its path, it is different from
# getSourceFile() name by not keeping the path in srcPath
#
method getFileName(f)
   local i, j:=0, path:=" "

   # what if the f is not in the current directory
   # such f = ../ipl/fname

   every j:= find("/",f)
   if (j+1) > *f then   fail
   else   return f[j+1:0]
end

#
# Extracts the file name from its path, it is only used by 
# loadSourceFiles()
#
method getSourceFileName(f)
   local i, j:=0, path:=" "
   local fname :="?"

   # what if the TP and UDB are not in the same directory
   # such as ./udb ./test/t1/t2/t

   every j:= find("/",f)

   path ||:= f[1:j+1]
   srcPath ||:= path ||" "
   if (j+1) > *f then   fail
   else   fname := f[j+1:0]

   return fname
end

#
# Obtains the file size from its name and path
#
method getSourceFileSize(f)
  local s:="", p, cmd :="du -h "
  local fsize:=-1, i

  # what about MS Windows ???     
  cmd ||:= f 
  p := open(cmd,"p")
  while s ||:=  read(p) || "\n"
  close(p)

  i := find("\t",s)
  fsize := s[1:\i]

  return fsize 
end

#
# Load the program Source File(s)
#
method loadSourceFiles(fname, src)
   local i, j, s, f, b, e

   exeName := getSourceFileName(fname)
   exeSize := getSourceFileSize(fname)
   fileNames := src
 
   # loads the source files that we can find and keep a 
   # list of the ones that we could not find. Note a source file
   # can be opened more that once so we need to be careful !!!
   every i := 1 to *fileNames do{
      f := getSourceFileName(fileNames[i])
      if not member(foundFiles, f) then{
         if s := dopen( fileNames[ i ] ) then {
            b := *srcText
            while( put( srcText, read( s ) ) )
            close( s )
            e := *srcText
            insert( foundFiles, f )
            srcIndex[ f ] := fileIndex(b,e)
            if /procMainFile then
               if procMainLine := searchFileProcs(f,"main") then
                  procMainFile := f
            }
         else
            put(missingFiles, fileNames[ i ])# keep it as it is
         }
      #else
         #write("File is already found:",f)
      }
   return
end

#
# Used by cmdBreak
# Searchs the fname for a specific linenum, and make sure it is applicable
# for a breakpoint condition, if it is , it returns that line source code.
#
method searchFileLines(fname, linenum)
   local temp, temp1, wSpace, punct

   wSpace := cset( " \t\n\^m" )
   punct := (",<>?~`!@$#^&*-_:=+|[]\'./;(){} \t\^m\"")
   temp := srcText[linenum + srcIndex[fname].fbegin]

   temp ? {
      tab(many(wSpace))
      if (temp1 := move(1)) then {
         if temp1 == "#" then {
            fail
            }
         return temp1 ||:= tab(many(CHARACTER ++ punct))
         }
      else{
         fail
         }
      }
end

#
# Used by cmdBreak
# Searchs the Fname for a specific ProcName and returns its linenum
#
method searchFileProcs(Fname, ProcName)
   local wSpace, other_char
   local temp, temp1, linenum

   if \Fname & \ProcName then{
      linenum := 1
      other_char := ("_")
      wSpace := cset( " \t\n\^m" )
      
      while temp := srcText[linenum + srcIndex[Fname].fbegin] do{
         temp ? {
            tab(many(wSpace))
            if(temp1 := tab(many(&letters))) == ("method"|"procedure") then{
              tab(many(wSpace))
              temp1 := tab(many(CHARACTER ++ other_char))
              if temp1 == ProcName then {
                 return linenum
                 }
              }
            }
         linenum +:= 1
         }
      }
   else
      fail
end

#
# returns the executable name
#
method getExeName()
   if exeName =="?" then
      fail
   return exeName 
end

#
# returns the File Name that has the procedure main 
#
method getMainFileName()
   if /procMainFile then
      fail
   return procMainFile
end

#
# returns the total number of lines in a file.
#
method getFileTotalLines(fname)
   /fname := procMainFile
    
   return srcIndex[fname].fend - srcIndex[fname].fbegin #+ 1
end

#
# checks whether fname is a source file or not
#
method isSrcFile(fname)
  if member(foundFiles, fname) | member(missingFiles, fname) then
     return
  fail
end

#
# check to see if file is loaded or not
# loaded files are those that opend and placed in srcText
#
method isLoaded(fname)
   if member(foundFiles, fname) then
      return
   fail
end

#
# checks to see whether a line number is in the file limits or not
#
method isInFile(fname, line)
   local b, e
   
   if line > 0 & isLoaded(fname) then{
      b := srcIndex[fname].fbegin
      e := srcIndex[fname].fend
      if (b + line <= e) & ( e - line >= b ) then
         return
      }
   fail
end

#
# returns an image of the current source line
#
method getSrcLine(fname, line)
    local f

    /fname := keyword("file", Monitored)
    /line  := keyword("line", Monitored)
    f := getFileName(fname)

    if isLoaded(fname) then #& isInFile(f, line) then       
       return srcText[line + srcIndex[ f ].fbegin]
    else
       fail
end

#
# returns the file name of a procedure 
#
method getProcFile(pname)
    local fname

    every fname := !foundFiles do
       if searchFileProcs(fname, pname) then
          return fname
    fail
end

#
# returns the line number of a procedure name
#
method getProcLine(pname)
    local pfile, pline

    if pfile := getProcFile(pname) then
       if pline := searchFileProcs(pfile, pname) then
          return pline
    fail
end

#
# prints out the source code
#
method getSrcCode(fname)
   local i, b, e, code:="\n"

   if /fname then fname := keyword("file", Monitored)

   b := srcIndex[fname].fbegin
   e := srcIndex[fname].fend
   every i:= b  to e do
      code ||:= srcText[i]||"\n"

   return code
end

#
# returns a string of the names of the opend src files 
#
method getFoundFiles()
   local L, x, i, max, fnames:="    "

   if *foundFiles > 0 then{
      i := 1
      L := sortf(foundFiles)
      max := maxlen(L)+2
      every x := !L do{
         fnames ||:=left(x, max)
         if i > 3 then{
            fnames ||:="\n    "
            i := 1    
            }
         else 
            i +:= 1
         } 
      return fnames
      }
   else
      fail  
end

#
# returns a string of the names of the missing src files
#
method getMissingFiles()
   local L, x, i, max, fnames:="    "

   if *missingFiles > 0 then{ 
      i := 1
      L := sortf(missingFiles)
      max := maxlen(L)+2
      every x := !L do{
         fnames ||:=left(x, max)
         if i > 3 then{
            fnames ||:="\n    "
            i := 1    
            }
         else 
            i +:= 1
         } 
      return fnames
      }
   else
      fail
end

#
# returns a string of the names of all source files
#
method getSourceFiles()
   local x, i, max, fnames:="    "
   
   if *fileNames > 0 then{
      i := 1
      max := maxlen(fileNames)+2
      every x := !fileNames do{
         fnames ||:=left(x, max)
         if i > 3 then{
            fnames ||:="\n    "
            i := 1    
            }
         else 
            i +:= 1
         } 
      return fnames
      }
   else
      fail
end

#
# returns the VM binary path (iconx, icont, wiconx, wicont)
#
method getBinPath()
   local f

   if (binPath := (&features ? (="Binaries at " & tab(0)))) |
      ((f := open((binPath:=getpaths()) ||
                  ("iconx"|"iconx.exe"|"wiconx.exe")))) then
      close(\f)
   write("binPath=",binPath)  
   return binPath
end

#
# returns the Unicon's library paths 
#
method getLibPath()
   local f

   /binPath := getBinPath()
$ifdef _UNIX   
   libPath := binPath || "../ipl/gdata"  || " " ||
              binPath || "../ipl/gincl"  || " " ||
              binPath || "../ipl/gprocs" || " " ||
              binPath || "../ipl/gprogs" || " " ||
              binPath || "../ipl/incl"   || " " ||
              binPath || "../ipl/lib"    || " " ||
              binPath || "../ipl/mincl"  || " " ||
              binPath || "../ipl/mprocs" || " " ||
              binPath || "../ipl/mprogs" || " " ||
              binPath || "../ipl/procs"  || " " ||
              binPath || "../ipl/progs"  || " " ||
              binPath || "../uni/lib"    || " " ||
              binPath || "../uni/gui" 
$else
   libPath := binPath || "..\ipl\gdata"  || " " ||
              binPath || "..\ipl\gincl"  || " " ||
              binPath || "..\ipl\gprocs" || " " ||
              binPath || "..\ipl\gprogs" || " " ||
              binPath || "..\ipl\incl"   || " " ||
              binPath || "..\ipl\lib"    || " " ||
              binPath || "..\ipl\mincl"  || " " ||
              binPath || "..\ipl\mprocs" || " " ||
              binPath || "..\ipl\mprogs" || " " ||
              binPath || "..\ipl\procs"  || " " ||
              binPath || "..\ipl\progs"  || " " ||
              binPath || "..\uni\lib"    || " " ||
              binPath || "..\uni\gui" 
$endif
end

#
# Searchs through the specified source path for the requested file.
# It looks first in current directory, 
# then it looks in the surrent direcory neigborhood, if it failed,
# it looks in the library paths 
#
method dopen(s)
   local File, paths, path, binpath, f

   if File := open(s) then {
      insert(userFiles, s)
      return File
      }
   else{
      paths := srcPath
      s := PS || s      # platform-specific
      paths ? {
         while path := tab(find(" ") | 0) do {
            if File := open(path || s) then {
               insert(userFiles, s)
               return File
               }
            tab(many(' ')) | break
            }
         }
      }

   # look in the library paths
   /libPath := getLibPath()

   paths := libPath
   s := PS || s      # platform-specific
   paths ? {
      while path := tab(find(" ") | 0) do {
         if File := open(path || s) then {
            insert(libFiles, s)
            return File
         }
         tab(many(' ')) | break
         }
      }
   fail
end

#
# Initialization
#
initially()

   exeName     := "?"
   exeSize     := -1

   fileNames   := []
   userFiles   := set()
   libFiles    := set()

   srcText     := []
   srcIndex    := table(0)
   foundFiles  := set()
   missingFiles:= []

$ifdef _UNIX
   srcPath   := ". .. ../.."
$else
   srcPath   := ". .. ..\.."
$endif
   #srcPath||:= " " || getenv( "DPATH" )
   srcPath ||:= " " || getenv( "LPATH" )
   srcPath ||:= " " || getenv( "IPATH" )
end

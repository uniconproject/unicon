import gui
link guiprocs
$include "guih.icn"

class BufferTabSet : TabSet()
   method get_tabitem(lab)
   local c
      if (c := !children).label == lab then return c
   end

   method display(buffer_flag)
      local last_on_a_line, cw

      #
      # Erase all and display outline of tabbed pane area.
      #
      EraseRectangle(cbwin, x, y, w, h)
      DrawRaisedRectangle(cbwin, x, y, w, h, 2)
      #
      # Display all tabs.
      #
      every (!!line_break).display_tab()
      last_on_a_line := (!line_break)[-1] === which_one

      #
      # Display line under tabs.
      #
      cw := Clone(cbwin, "drawop=reverse")
      Fg(cw, "pale gray")
      DrawLine(cw, x, y + tab_h - 2, which_one.label_x, y + tab_h - 2)

      if /last_on_a_line then
         DrawLine(cw, which_one.label_x + which_one.label_w, y + tab_h - 2,
                  x + w - 1, y + tab_h - 2)

      Fg(cw, "black")
      DrawLine(cw, x, y + tab_h - 1, which_one.label_x, y + tab_h - 1)

      if /last_on_a_line then
         DrawLine(cw, which_one.label_x + which_one.label_w, y + tab_h - 1,
                  x + w - 2, y + tab_h - 1)

      Uncouple(cw)

      #
      # Display contents of current tab into buffer
      #
      which_one.display(1)
      do_shading(cbwin)

      if /buffer_flag then
         CopyArea(cbwin, cwin, x, y, w, h, x, y)
   end

   #
   # Set which tab is currently on display.
   # @param x  The {TabItem} to be displayed.
   #
   # Functionality added to set focus in editbox
   # associated with current tab item
   #
   method set_which_one(x)
      self.TabSet.set_which_one(x)
      uidlog.set_focus(ide.CurrentEditBox())
      return x
   end

   method tab_right(c)
      local i
      if *components = 1 then fail
      every i := 1 to *components do if components[i] === c then break
      if (i > *components) | (components[i] ~=== c) then fail
      set_which_one( components[ (i % *components) + 1 ] )
   end

initially
   self$TabSet.initially()
end

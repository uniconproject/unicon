#
# $Id: canvassizer.icn,v 1.3 2006-07-09 23:43:13 rparlett Exp $
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a Sizer on the canvas.
#
class CanvasSizer : CanvasComponent : Sizer()
   #
   # Overrides CanvasComponent.min_height()
   #
   method min_height()
      return 3
   end

   #
   # Overrides CanvasComponent.min_width()
   #
   method min_width()
      return 3
   end

   #
   # Generate code into Code object c.
   #
   method gen_code(c)
      local s
      self.CanvasComponent.gen_code(c)
      if \self.lo | \self.hi then {
         s := name || ".set_range(" || (\self.lo|"&null") || ", " ||
            (\self.hi|"&null") || ")" 
         c.line(s)
      }
      if \self.is_horizontal_flag then
         c.line(name || ".set_is_horizontal()")
   end

   #
   # Copy fields from self to c.
   #
   method cp_fields(c)
      c.is_horizontal_flag := self.is_horizontal_flag
      c.lo := self.lo
      c.hi := self.hi
   end

   #
   # Copy object.
   #
   method cp()
      local c
      c := CanvasSizer()
      self.CanvasComponent.cp(c)
      self.cp_fields(c)
      return c
   end

   #
   # Duplicate object.
   #
   method dup(pc)
      local c
      c := CanvasSizer()
      self.CanvasComponent.dup(c, pc)
      self.cp_fields(c)
      return c
   end

   #
   # Return the I/O template.
   #
   method get_template()
      return self.CanvasComponent.get_template() ||| 
         [
          ["Lo", "lo"],
          ["Hi", "hi"],
          ["Is horizontal flag", "is_horizontal_flag"]
          ]
   end

   #
   # Open the configuration dialog box.
   #
   method dialog()
      return CanvasSizerDialog(self)
   end

   initially
      self.CanvasComponent.initially()
      self.Sizer.initially()
      self.class_name := "Sizer"
      self.import_name := "gui"
      self.name := "sizer"
end

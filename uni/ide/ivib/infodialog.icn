#
# $Id: infodialog.icn,v 1.3 2004-05-08 21:04:29 rparlett Exp $
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui
$include "guih.icn"

#############################################################################
#
# A simple dialog to output a multi-line message.
#     
class InfoDialog : CommonDialog(lines, 
                                title,
                                width,
                                button_text,
                                alignment,
                                spacing,
                                close_button)
   method component_setup()
      local p, s, l, y

      /spacing := 20
      /width := 320
      /alignment := "c"
      /button_text := "Close"

      self.set_attribs("size=" || width || "," || 90 + *lines * spacing, 
                       "label=" || title)
      close_button := TextButton()
      close_button.set_pos("50%", "100%-30")
      close_button.set_align("c", "c")
      close_button.set_internal_alignment("c")
      close_button.set_label(button_text)
      close_button.connect(self, "dispose", ACTION_EVENT)
      self.add(close_button)
      p := Panel()
      p.set_pos(0, 0)
      p.set_size("100%", "100%-60")
      y := 20 + spacing / 2
      every s := !lines do {
         l := Label()
         if alignment == "c" then {
            l.set_align("c", "c")
            l.set_pos("50%", y)
         } else {
            l.set_align("l", "c")
            l.set_pos(15, y)
         }
         l.set_label(s)
         p.add(l)
         y +:= spacing
      }
      self.add(p)
      set_focus(close_button)
   end
end

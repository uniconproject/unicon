package ulsp

import json

class SignatureHandler(
   workspace,
   params
   )

   method setVariables(new_workspace, new_params)
      workspace := new_workspace
      params := new_params
   end

   method run()
      local line, character, methodName, results_table

      # Collect line and character position #
      line := params["position"]["line"]
      character := params["position"]["character"]


      # Collect context information (inside comment, space before, object before, no object before) #
      _context := getContext(line, character, workspace.temp_file)
      \_context | fail

      # Observe context #
      write(_context)

      # Create initial return table#
      results_table := table()
      results_table["signatures"] := []

      case _context of {

         # If object before, collect object information #
         "object before" : {
            methodName := getMethod(line, character, workspace.temp_file)
            objectName := getObject(line, character, workspace.temp_file)
         } 

         # If no object before, treat as method inside class, internal procedure, or built-in-funtion (eventually) #
         "no object before" : { 
            if _context == "no object before" then {

               methodName := getMethod(line, character, workspace.temp_file)
               internal_classes := workspace.getInternalClasses()

               # Only checking to see if method is in an internal class currently #
               every _class := workspace.getFile().getClasses().get() do {
                  if member(internal_classes[_class.getName()]["methods"], methodName) then {

                     # Create a valid signature #
                     signature := createSignature(methodName, internal_classes[_class.getName()]["methods"][methodName])

                     # Put method params in result table #
                     put(results_table["signatures"], table("label", signature, "parameters", []))
                     every paramName := !internal_classes[_class.getName()]["methods"][methodName]["params"] do 
                        put(results_table["signatures"][1]["parameters"], table("label", paramName))

                     # Observe results_table #
                     write(ximage(results_table))
                  }
               }
            }
         }
      }

      # Convert and return finished table #
      results := tojson(results_table)
      return results
   end

   # Create a valid signature #
   method createSignature(function, functionTable)
      local signature, counter

      counter := 0

      signature := function || "("
      every paramName := !functionTable["params"] do {
         if counter < *functionTable["params"] - 1 then
            signature ||:= paramName || ", "
         else
            signature ||:= paramName
         counter +:= 1
         }
      signature ||:= ")"

      return signature
   end

   # Retriving context #
   method getContext(line, character, file)
      desired_line := getDesiredLine(line, file)

      \desired_line | fail

      desired_line ? {
         while (&pos < character) do {
            ch := move(1)
            if ch == "#" then 
               return "inside comment"
            }
         }

      desired_line ? {
         &pos := character
         if move(-1) == " " then 
            return "space before"
         }
      
      desired_line ? {
         &pos := character
         until desired_line[&pos] == " " | desired_line[&pos] == "." | &pos = 1 do move(-1)
         if desired_line[&pos] == "." & desired_line[&pos -1] ~== " " then 
            return "object before"
      }
      return "no object before"
   end

   # Retriving method name #
   method getMethod(line, character, file)
      desired_line := getDesiredLine(line, file)

      desired_line ? {
         &pos := character

         until desired_line[&pos] == " " | desired_line[&pos] == "."  | &pos = 1 do move(-1)
         if desired_line[&pos] == " " | desired_line[&pos] == "." then move(1)

         return tab(upto("("))
      }
   end

   # Retriving object name #
   method getObject(line, character, file)
      desired_line := getDesiredLine(line, file)

      desired_line ? {
         &pos := character

         until desired_line[&pos] == " " | &pos = 1 do move(-1)
         if desired_line[&pos] == " " then move(1)

         return tab(upto("."))
      }
   end

   # Returning line from temp file as string #
   method getDesiredLine(line, file)
      f := open(file)
      every !line do read(f)
      desired_line := read(f)
      return desired_line
   end
end
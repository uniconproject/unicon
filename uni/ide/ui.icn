###########################################################################
#
#	File:     ui.icn
#
#	Subject:  Unicon IDE, an integrated programming environment
#
#	Authors:  Hani Bani Salameh, Nolan Clayton and Clinton Jeffery
#
#	Date:     July 2, 2007
#
############################################################################
#
#  This program provides a simple multi-platform development environment
#  for Unicon.  It is gradually gaining features and usability.
#
#  To do:
#	keyboard shortcuts
#	multiple edit regions.
#	
############################################################################
#
#  Requires:  graphics, version 2 GUI libraries, and ivib
#
############################################################################
#
#  Links:  graphics, io, font_dlg
#
############################################################################

import gui
$include "guih.icn"
link mainbar
link getpaths
link classbrowser
link enqueue, preferences

$ifdef _MS_WINDOWS_NT
$define SL "\\"
$else
$define SL "/"
$endif

#
# You may alter your makedefs and/or makefile to add directories in which
# to find these files to your IPATH.
#
link unigram, idol, tree, preproce, unilex, unicon, yyerror

global linebreak, uidlog, browser, docpath,
       current_dir, is_MSWindows, PATHSEP, env, win_position,
       fontname

#$define lpr "enscript -b \" \" -1 -Plex3normal"
##$define HELP_PATH "help"

class UIDE : Dialog(
   #
   # The class variables, generally representing components in the dialog.
   #
   uide,
   startfile,
   pnlToolbar, menubar,
   MsgBox, EditBox,
   FileTabSet,ClassTabSet, DevTabSet,
   FileBrowser,
   tab_item_1, tab_item_2, tab_item_3, tab_item_4,
   ui_Preferences
   )

   method component_setup()
      local error_msg := pre_ui_initialize()
      self.setup()
      pnlToolbar := Panel()
      pnlToolbar.set_pos("0", "4.5%")
      pnlToolbar.set_size("100%", "4.5%")
      pnlToolbar.set_draw_border()
      pnlToolbar.set_attribs("bg=light whitish gray", "resize=on")
      self.add(pnlToolbar)
      menubar.Menubar()
      ide.Toolbar(pnlToolbar) #This call to add a toolbar
      FileBrowser.addreplace("file_contents", "tab_label", 0)
      MsgBox.set_contents([\error_msg])
   end

   method IDEDialog()
   end

   #
   # Set Doc Path Proc:
   #
   method set_docpath()
      match("Binaries at ", docpath := &features) |
      Notice("What, no binaries?" ||
             "Have you added Unicon's bin directory to your path?")
      docpath ?:= (="Binaries at " & tab(0))
      if map(docpath[-4:0])==("bin/"|"bin\\") then docpath[-4:0] := ""
      else write("did not find expected Unicon binaries in a bin/ directory")
   end

   #
   # pre initialization procedure
   #
   method pre_ui_initialize()
      local blank_window, error_msg
      ui_Preferences := DialogPreferences()
      error_msg := menubar.load_config()
      if \ui_Preferences.paren_check_box_state == "" then
	  ui_Preferences.paren_check_box_state := &null
      if \ui_Preferences.syntax_check_box_state == "" then
	  ui_Preferences.syntax_check_box_state := &null
      if not (blank_window := open("u_ide_blank", "g", "canvas=hidden")) then
	 fail
      if /win_position then win_position := "2,3"
      if /height then height := WAttrib(blank_window, "displayheight") - 150
      if /width then width := WAttrib(blank_window, "displaywidth") - 150
      close(blank_window)
      if /msglines then msglines := 7
      if /fontname then fontname := "sans,15"
      if /linebreak then linebreak := "LF"  # default to UNIX mode
      if \error_msg then return error_msg
   end

   #
   # main initialization procedure
   #
   method ui_initialize()
      local f, m, line
      menubar.on_hide_toolbar() # hide/show toolbar and setup ide window layout
      env := getenv("IDOLENV") | "uniclass"
      alpha    := &ucase ++ &lcase ++ '_' ++ &digits
      nonalpha := &cset -- alpha
      alphadot := alpha ++ '.'
      white    := ' \t\f\r'
      nonwhite := &cset -- white

      wiconlog := tempname("ui",".tmp",,)
      xargs := ""
      if \MsgBox.contents then {
         m := ""
         every m ||:= !!MsgBox.contents
	 }
      if /is_MSWindows then {
         every browser := getenv("BROWSER") | "chromium-browser" |
			  "firefox" | "netscape" | "mozilla" | "opera" do {
	    if stat(getpaths() || browser) then {
	       MsgBox.set_contents(["found browser " || browser, \m])
	       break
	       }
	    else browser := &null
	    }
	 }
      else {
         (browser := "\""|| (
	     WinAssociate("HTM") |
             (getenv("ProgramW6432"|"ProgramFiles"|"ProgramFiles(x86)") ||
			"\\Internet Explorer\\iexplore.exe")) || "\"") |
            write("What, no browser?  Unicon's online help won't be readable \n_
             until you associate an HTML reader application with the HTM \n_
             file extension.\n")
      }
      # iexplore runs off http:// but not file://
      if (not find("iexplore.exe", \browser)) |
	 (not match("online", is_online())) then set_docpath()
#      if not verify_make() then on_close()
   end

   method end_dialog()
   end

   method init_dialog()
   local wid, ht
      ui_initialize()
#      docpath := &null
      insert(opened_files_set,"scratch")
      ide.handle_openfile(&null, \startfile)
      menubar.set_font()
      # queue up an lpress to set the initial focus to the editor buffer
      if \win then {
	 Enqueue(\win, &lpress, EditBox.x+2, EditBox.y+2)
	 }
   end

# Proposed function key bindings. Still under construction.
# Loosely based on Borland-style IDE keybindings.
#
#  Key	Base		Control     Alt		Shift
#  F1   Help		search@crsr help/srch	help index
#  F2	Save file	restart	    Find(next)	grep
#  F3	Open file	callstack   closewin	New file
#  F4   Run to cursor
#  F5	Toggle breakpt		    User screen
#  F6	Next win/tab				Previous win
#  F7	Step		set watchpt
#  F8	Next		set breakpt
#  F9	Make		Run	    Compileonly Run/debug
#  F10	Main menu		
#  F11	runtime msgs	project dlg inspect
#  F12	compiler msgs	toggle ivib
# alt-x = exit


   #
   # Generic implementation
   #
   method on_function_key(ev)
      case ev.param of {
	 Key_F1:   ide_key_file_menu()
	 Key_F2:   ide_key_help_menu()
	 Key_F3:   ide_handle_save()
	 Key_F4:   ide_handle_open()
	 Key_F5:   ide_prev_tab()
	 Key_F6:   ide_next_tab()
	 Key_F7:   ""#ide_handle_compile()
	 Key_F8:   ""#ide_handle_run()
	 Key_F9:    ""
	 Key_F10:   ""
	 Key_F11:   ""
	 Key_F12:  ide_handle_exit()
	 }
   end

   #
   # compile and run are not reliable enough yet to
   # implement these shortcuts
   #
   method ide_handle_compile()
      ide.on_makeexe_btn()
   end

   method ide_handle_run()
      ide.on_run_btn()
   end

   method ide_handle_exit()
      menubar.on_exit()
   end

   method ide_handle_open()
      ide.on_open_btn()
   end

   method ide_handle_save()
      ide.on_save_btn()
      ide.CurrentEditBox().handle_event()
    end

   method ide_next_tab()
      local i, fs, cs, ct
      fs := FileTabSet
      cs := FileTabSet.children
      ct := ide.CurrentTabItem()

      if  *cs = 1 then fail
      every i := 1 to *cs do if cs[i] === ct then break
      if (i > *cs) | (cs[i] ~=== ct) then fail
      if cs[*cs] === ct then
         fs.set_which_one(cs[1])
      else
         fs.set_which_one(cs[i+1])
   end

   method ide_prev_tab()
      local i, fs, cs, ct
      fs := FileTabSet
      cs := FileTabSet.children
      ct := ide.CurrentTabItem()

      if  *cs = 1 then fail
      every i := 1 to *cs do if cs[i] === ct then break
      if (i > *cs) | (cs[i] ~=== ct) then fail
      if cs[1] === ct then
         fs.set_which_one(cs[-1])
      else
         fs.set_which_one(cs[i-1])
   end

   method ide_key_file_menu()
      local m, b, r
      m := menubar.menu_bar_1.menus
      b := menubar.menu_bar_1
      uidlog.set_focus(b)
#      if not b.has_focus then
#         set_focus(b)
      if (r := !m).label == "File" then {
         if \b.which_open then {
            if b.which_open === r then
               b.set_which_open()
            else
               b.set_which_open(r)
            }
         else {
            b.set_which_open(r)
            }
       #  self.unique_end()
         }
   end

   method ide_key_help_menu()
      local m, b, r
      m := menubar.menu_bar_1.menus
      b := menubar.menu_bar_1
      uidlog.set_focus(b)
#      if not b.has_focus then
#         set_focus(b)
      if (r := !m).label == "Help" then {
         if \b.which_open then {
            if b.which_open === r then
               b.set_which_open()
            else
               b.set_which_open(r)
            }
         else {
            b.set_which_open(r)
            }
      #   self.unique_end()
         }
   end

   method on_close(ev)
      menubar.on_exit(ev)
   end

   method change(e)
#      write("The tabset selection changed.")
   end

   method setup()
      self.min_width := 645
      self.min_height := 485
      self.set_attribs("size="||width||","||height, "pos="||win_position,
		       "bg=pale gray",
		       "label=Unicon IDE", "font=sans,15", "resize=on")

      DevTabSet := TabSet()
      DevTabSet.set_pos("0", "100%-110")
      DevTabSet.set_size("100%", "110")
      tab_item_1 := TabItem()
      tab_item_1.set_label("Messages: (Single File Mode)")
      MsgBox := EditableTextList()
      MsgBox.set_pos("2", "2")
      MsgBox.set_size("100%-4", "100%-4")
      MsgBox.set_draw_border()
      MsgBox.set_attribs("bg=white", "font="||fontname)
      MsgBox.set_contents([""])
      tab_item_1.add(MsgBox)

      EditBox := BuffEditableTextList()
      EditBox.set_pos("2", "2")
      EditBox.set_size("100%-4", "100%-4")
      EditBox.set_draw_border()
      EditBox.set_attribs("bg="||ui_Preferences.val_background_color,
			  "font="||fontname)
      EditBox.set_contents([""])

      DevTabSet.add(tab_item_1)
      DevTabSet.set_which_one(tab_item_1)
      self.add(DevTabSet)
      FileTabSet := BufferTabSet()
      FileTabSet.set_pos("150", "9%")
      FileTabSet.set_size("100%-150", "100%-160")
      tab_item_2 := BufferTabItem()
      tab_item_2.set_label("scratch")
      tab_item_2.add(EditBox)
      FileTabSet.add(tab_item_2)
      FileTabSet.set_which_one(tab_item_2)
      self.add(FileTabSet)
      ClassTabSet := BufferTabSet()
      ClassTabSet.set_pos("0", "9%")
      ClassTabSet.set_size("150", "100%-160")
      tab_item_3 := TabItem()
      tab_item_3.set_label("Class Browser")
      FileBrowser := ClassBrowser()
      FileBrowser.set_pos("2", "2")
      FileBrowser.set_size("100%-4", "100%-4")
      FileBrowser.set_draw_border()
      FileBrowser.set_attribs("bg=white")
      FileBrowser.set_select_one()
      tab_item_3.add(FileBrowser)
      ClassTabSet.add(tab_item_3)
      ClassTabSet.set_which_one(tab_item_3)
      self.add(ClassTabSet)
      pnlToolbar := Panel()
      pnlToolbar.set_pos("0", "4.5%")
      pnlToolbar.set_size("100%", "4.5%")
      self.add(pnlToolbar)
      self.connect(self, "on_close", CLOSE_BUTTON_EVENT)
   end

initially(initfile)
   self.Dialog.initially()

   # contains the main ide functions
   ide := IDE(FileTabSet, ClassTabSet, FileBrowser)
   uidlog := self
   menubar := Mainbar(UProject())
   startfile := initfile
   return self
end

#
# Primitive legacy Notice/TextDialog compatibility functions.
# They need to be improved/polished and eventually moved to the gui package.
#

global dialog_value, dialog_button

$define ButtonWidth	 50	# minimum button width
$define ButtonHeight	 30	# button height
$define FieldWidth	 20	# default field width
$define OpenWidth	 50	# default field width for Open/SaveDialog

$define XOff		  0	# offset for text vidgets
$define XOffButton	 85	# initial x offset for buttons
$define XOffIncr	 15	# space between buttons

class vdialog : Dialog(captions, labels, buttons, textfields,
		       defaults, myanswer)
   method component_setup()
      self.setup()
   end

   method end_dialog()
   end

   method init_dialog()
   local maxw
      maxw := 0
      every maxw <:= TextWidth(win, !captions)
      maxw +:= 40
      maxw <:= 220
      self.resize_win(maxw, (32 * (*captions + *labels +2)))
      if textfields[1] then {
	 Enqueue(\win, &lpress, textfields[1].x+2, textfields[1].y+2)
	 }
   end

   method setup()
   local i, cap
   local nlines := *captions + *labels + 2
   local label_2, text_button_1, text_field_1

      self.set_attribs("size=220," || (32 * nlines))
      every i := 1 to *captions do {
	 cap := Label()
	 cap.set_pos("20", integer(i *100 / nlines)|| "%-5")
	 cap.set_internal_alignment("l")
	 cap.set_label(captions[i])
	 self.add(cap)
	 }
      every i := 1 to *labels do {
	 label_2 := Label()
	 label_2.set_pos(20, integer((*captions + i)*100/nlines) || "%")
	 label_2.set_internal_alignment("l")
	 label_2.set_label(labels[i])
	 self.add(label_2)
	 }
      every i := 1 to *labels do {
	 text_field_1 := TextField()
	 text_field_1.set_pos(20, integer((*captions + i)*100/nlines) || "%-10")
	 text_field_1.set_size(180, 30)
	 text_field_1.set_contents((\defaults)[i])
	 text_field_1.set_draw_border()
         if dialogFor == "find" then
	    text_field_1.set_contents(findstr)
         else if dialogFor == "goto" then
	    text_field_1.set_contents(gotoln)
         else if dialogFor == "compile" then
	    text_field_1.set_contents(targs)
         else if dialogFor == "run" then
	    text_field_1.set_contents(xargs)
         else text_field_1.set_contents("")
	 text_field_1.connect(self, "on_keypress", ACTION_EVENT)

	 self.add(text_field_1)
	 put(textfields, text_field_1)
	 }
      every i := 1 to *buttons do {
	 text_button_1 := TextButton()
	 text_button_1.set_pos( integer(i * 100 / (*buttons+1)) || "%-20",
			       integer((*captions + *labels + 1)*100/nlines) ||
			       "%" )
	 text_button_1.connect(self, "on_button", ACTION_EVENT)
	 text_button_1.clear_toggles()
	 text_button_1.set_label(buttons[i])
	 text_button_1.set_internal_alignment("c")
	 self.add(text_button_1)
	 }
   end

   method on_button(ev)
      myanswer := ev.source.label
      dispose()
   end

   method on_keypress(ev)
      myanswer := buttons[1]
      dispose()
   end

initially(caps, labs, btns, dflts)
   captions := caps
   labels := labs
   buttons := btns
   defaults := dflts
   textfields := []
   self.Dialog.initially()
end


procedure TextDialog(			#: text dialog
   win, captions, labels, defaults, widths, buttons, index
   )
local default_width, default_button, maxl, id, label_width, maxb, lead, pad,
      cwidth, dialog, maxw, y, temp_win

   if type(win) ~== "window" then {
      win :=: captions :=: labels :=: defaults :=: widths :=: buttons :=:
         index
      win := &window
      /win := temp_win := WOpen("canvas=hidden", "bg=gray")
      }

   /captions := []
   /labels := []
   /defaults := []
   /widths := []
   /buttons := ["Okay", "Cancel"]
   /index := 1

   if type(captions) ~== "list" then captions := [captions]
   if type(labels) ~== "list" then labels := ([\labels] | [])
   if type(defaults) ~== "list" then defaults := ([\defaults] | [])
   if type(widths) ~== "list" then widths := ([\widths] | [default_width])
   if type(buttons) ~== "list" then buttons := [buttons]

   default_button := buttons[index] 		# null if out of bounds
   default_width := widths[-1] | FieldWidth

   maxl := 0
   every maxl <:= *(labels | defaults | widths)
   until *labels = maxl do put(labels, labels[-1] | "")
   until *defaults = maxl do put(defaults, defaults[-1] | "")
   until *widths = maxl do put(widths, widths[-1] | 10)

   id := 0

   label_width := 0
   every label_width <:= TextWidth(win, !labels)
   if label_width > 0 then label_width +:= 15

   maxb := 0
   every maxb <:= TextWidth(win, !buttons)
   maxb +:= 10
   maxb <:= ButtonWidth

   lead := WAttrib(win, "leading")
   pad := 2 * lead
   cwidth := WAttrib(win, "fwidth")

   dialog := vdialog(captions, labels, buttons, defaults)

   maxw := 0
   every maxw <:= TextWidth(win, !captions)

   y := -lead

   dialog.show_modal()
   dialog_button := dialog.myanswer
   dialog_value := []
   every put(dialog_value, (!(dialog.textfields)).get_contents())
   return dialog_button
end

procedure Notice(captions[])		#: notice dialog
   local win, temp_win, p
   static type

   initial type := proc("type", 0)	# protect attractive name

   if type(captions[1]) == "window" then
      win := get(captions)
   else {
      win := &window
      /win := temp_win := WOpen("canvas=hidden", "bg=gray")
      }

   # use built-in if available
   if \is_MSWindows & (p := proc("WinSelectDialog", 0)) then {
      p(win, captions)
      dialog_button := "Okay"
      }
   else
      TextDialog(win, captions, , , , "Okay")

   dialog_value := &null

   WClose(\temp_win)

   return dialog_button

end

# outside of package UniconPackage, an IDE warning should be in the messagebox
procedure ui_Warning(s, lineno, filenam)
   local msg := ""
   if \filenam then msg := msg || filenam || ":"
   if \lineno then msg := msg || lineno || ":"
   msg ||:= ": " || s
   uidlog.MsgBox.set_contents([msg])
end

procedure ui_Progend(x)
    fail
end

procedure main(argv)
   local d := argv[1]
   reinitialize()
   Progend := ui_Progend
   warning := ui_Warning

   if &features=="MS Windows NT" then {
      PATHSEP := "\\"
      is_MSWindows := 1
      }
   else {
      PATHSEP := "/"
      is_MSWindows := &null
      }

   d := UIDE(d)
   d.show_modal()
end

### Ivib-v2 layout ##
#class|Canvas|17|Name Table|table|integer|0|11|string|editable_text_lis
#t|integer|2|string|icon|integer|1|string|menu|integer|2|string|menu_ba
#r|integer|1|string|menu_separator|integer|1|string|panel|integer|1|str
#ing|tab_item|integer|4|string|tab_set|integer|3|string|text_list|integ
#er|2|string|text_menu_item|integer|18|string|tree|integer|1|SuperClass
# Name|string|Dialog|Import Name|string|gui|Button Groups|class|ButtonG
#roupSet|2|Parent Canvas|1|Boxes|list|0|Checkbox Groups|class|CheckBoxG
#roupSet|2|Parent Canvas|1|Boxes|list|0|Gen Indent|integer|3|Gen Main|i
#nteger|1|Gen Component Setup|integer|1|Gen Init Dialog|integer|1|Gen I
#nitially|integer|1|Dialog Struct|class|CDialog|4|Min Width|null|Min He
#ight|null|Ticker Rate|null|Attribs|list|4|string|bg=pale gray|string|l
#abel=Unicon IDE|string|font=sans,15|string|resize=on|Name|string|UIDE|
#Width|integer|750|Height|integer|560|Items|list|4|class|CanvasTabSet|2
#7|Parent Canvas|1|Name|string|DevTabSet|Class Name|string|TabSet|Impor
#t Name|string|gui|X Fix|null|Y Fix|null|W Fix|null|H Fix|null|W Defaul
#t|null|H Default|null|X Spec|string|0|Y Spec|string|100%-110|W Spec|st
#ring|100%|H Spec|string|110|X Align|string|l|Y Align|string|t|Is shade
#d|null|Is Button Subclass|null|Draw Border|null|Attribs|list|0|Tooltip
#|null|Accel|null|Event Handlers|list|0|Class Variable|integer|1|Parent
# Component|1|Children|list|1|class|CanvasTabItem|28|Parent Canvas|1|Na
#me|string|tab_item_1|Class Name|string|TabItem|Import Name|string|gui|
#X Fix|null|Y Fix|null|W Fix|null|H Fix|null|W Default|null|H Default|n
#ull|X Spec|integer|0|Y Spec|integer|0|W Spec|string|100%|H Spec|string
#|100%|X Align|string|l|Y Align|string|t|Is shaded|null|Is Button Subcl
#ass|null|Draw Border|null|Attribs|list|0|Tooltip|null|Accel|null|Event
# Handlers|null|Class Variable|integer|1|Parent Component|10|Label|stri
#ng|Messages: (Single File Mode)|Parent TabSet|10|Children|list|1|class
#|CanvasEditableTextList|26|Parent Canvas|1|Name|string|MsgBox|Class Na
#me|string|EditableTextList|Import Name|string|gui|X Fix|null|Y Fix|nul
#l|W Fix|null|H Fix|null|W Default|null|H Default|null|X Spec|string|2|
#Y Spec|string|2|W Spec|string|100%-4|H Spec|string|100%-4|X Align|stri
#ng|l|Y Align|string|t|Is shaded|null|Is Button Subclass|null|Draw Bord
#er|integer|1|Attribs|list|1|string|bg=white|Tooltip|null|Accel|null|Ev
#ent Handlers|list|0|Class Variable|integer|1|Parent Component|14|Conte
#nts|list|1|string||Which One|14|class|CanvasTabSet|27|Parent Canvas|1|
#Name|string|FileTabSet|Class Name|string|BufferTabSet|Import Name|stri
#ng|gui|X Fix|null|Y Fix|null|W Fix|null|H Fix|null|W Default|null|H De
#fault|null|X Spec|string|150|Y Spec|string|9%|W Spec|string|100%-150|H
# Spec|string|100%-160|X Align|string|l|Y Align|string|t|Is shaded|null
#|Is Button Subclass|null|Draw Border|null|Attribs|list|0|Tooltip|null|
#Accel|null|Event Handlers|list|0|Class Variable|integer|1|Parent Compo
#nent|1|Children|list|1|class|CanvasTabItem|28|Parent Canvas|1|Name|str
#ing|tab_item_2|Class Name|string|TabItem|Import Name|string|gui|X Fix|
#null|Y Fix|null|W Fix|null|H Fix|null|W Default|null|H Default|null|X 
#Spec|integer|0|Y Spec|integer|0|W Spec|string|100%|H Spec|string|100%|
#X Align|string|l|Y Align|string|t|Is shaded|null|Is Button Subclass|nu
#ll|Draw Border|null|Attribs|list|0|Tooltip|null|Accel|null|Event Handl
#ers|null|Class Variable|integer|1|Parent Component|21|Label|string|scr
#atch|Parent TabSet|21|Children|list|1|class|CanvasEditableTextList|26|
#Parent Canvas|1|Name|string|EditBox|Class Name|string|BuffEditableText
#List|Import Name|string|gui|X Fix|null|Y Fix|null|W Fix|null|H Fix|nul
#l|W Default|null|H Default|null|X Spec|string|2|Y Spec|string|2|W Spec
#|string|100%-4|H Spec|string|100%-4|X Align|string|l|Y Align|string|t|
#Is shaded|null|Is Button Subclass|null|Draw Border|integer|1|Attribs|l
#ist|1|string|bg=white|Tooltip|null|Accel|null|Event Handlers|list|0|Cl
#ass Variable|integer|1|Parent Component|25|Contents|list|1|string||Whi
#ch One|25|class|CanvasTabSet|27|Parent Canvas|1|Name|string|ClassTabSe
#t|Class Name|string|BufferTabSet|Import Name|string|gui|X Fix|null|Y F
#ix|null|W Fix|null|H Fix|null|W Default|null|H Default|null|X Spec|str
#ing|0|Y Spec|string|9%|W Spec|string|150|H Spec|string|100%-160|X Alig
#n|string|l|Y Align|string|t|Is shaded|null|Is Button Subclass|null|Dra
#w Border|null|Attribs|list|0|Tooltip|null|Accel|null|Event Handlers|li
#st|0|Class Variable|integer|1|Parent Component|1|Children|list|1|class
#|CanvasTabItem|28|Parent Canvas|1|Name|string|tab_item_3|Class Name|st
#ring|TabItem|Import Name|string|gui|X Fix|null|Y Fix|null|W Fix|null|H
# Fix|null|W Default|null|H Default|null|X Spec|integer|0|Y Spec|intege
#r|0|W Spec|string|100%|H Spec|string|100%|X Align|string|l|Y Align|str
#ing|t|Is shaded|null|Is Button Subclass|null|Draw Border|null|Attribs|
#list|0|Tooltip|null|Accel|null|Event Handlers|null|Class Variable|inte
#ger|1|Parent Component|32|Label|string|Class Browser|Parent TabSet|32|
#Children|list|1|class|CanvasTree|29|Parent Canvas|1|Name|string|FileBr
#owser|Class Name|string|ClassBrowser|Import Name|string|gui|X Fix|null
#|Y Fix|null|W Fix|null|H Fix|null|W Default|null|H Default|null|X Spec
#|string|2|Y Spec|string|2|W Spec|string|100%-4|H Spec|string|100%-4|X 
#Align|string|l|Y Align|string|t|Is shaded|null|Is Button Subclass|null
#|Draw Border|integer|1|Attribs|list|1|string|bg=white|Tooltip|null|Acc
#el|null|Event Handlers|list|0|Class Variable|integer|1|Parent Componen
#t|36|Select One|integer|1|Select Many|null|Show Root Handles|integer|1
#|Show Root|integer|1|Which One|36|class|CanvasPanel|26|Parent Canvas|1
#|Name|string|pnlToolbar|Class Name|string|Panel|Import Name|string|gui
#|X Fix|null|Y Fix|null|W Fix|null|H Fix|null|W Default|null|H Default|
#null|X Spec|string|0|Y Spec|string|4.5%|W Spec|string|100%|H Spec|stri
#ng|4.5%|X Align|string|l|Y Align|string|t|Is shaded|null|Is Button Sub
#class|null|Draw Border|null|Attribs|list|0|Tooltip|null|Accel|null|Eve
#nt Handlers|list|0|Class Variable|integer|1|Parent Component|1|Childre
#n|list|0|Initial Focus|null|Event Handlers|list|1|list|2|string|CLOSE_
#BUTTON_EVENT|string|on_close|

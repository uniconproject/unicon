#
# uget.icn - Unicon version of wget.
#
# Authors: Phillip Thomas and Jafar Al-Gharaibeh
#
# Date: July 2018
#

link basename
link options

procedure usage()
    write("usage: uget [options] URI")
    write("options:")
    write("\t -n : no https certificate verification")
    write("\t -f : force overwrite existing file")
    write("\t -v : verbose output")
    write("\t -h : show html header only, don't save the file")
    write("\t -p : print the content to the console, don't save to file")
    exit(-1)
end

procedure main(args)
    local opts, url, web, fname, mode
    opts := options(args, "nfvhp")
    url := args[1] | usage()
    if \opts["n"] then
	mode := "m-" # don't verify https certificates 
    else
	mode := "m"
    
    if ssl := match("https", url) then {
	if not (&features == "secure sockets layer encryption") then
	    stop("This Unicon build does not support https")  
    }
    else
	match("http", url) | usage()

     repeat {
	web := open(url, mode) | stop("can't open URL ", url)
	if \opts["v"] | \opts["h"] then
	    showheaders(web)

	if web["Status-Code"] = 301 then {
	    url := web["Location"]
	    write(left("Redirect", 16), ": ", image(url))
	    close(web)
	}
	else
	    break
    }

    if web["Status-Code"] ~= 200 then
	stop("url status: " || web["Status-Code"])

    if /opts["h"] then {

	if /opts["p"] then {
	    fname := basename(url)	
	    write("saving to ", fname)
	    if stat(fname) & \opts["f"] then stop("file already exists, use -f to overwrite")
	    fout := open(fname, "w") | stop("can't open/write ", fname)
	}
	
	size := integer(web["Content-Length"])
	write("size:", size)
	t := &time
	line := reads(web, size) | stop("Can't reads")
	write(fout, line)
	close(\fout)
	write("time:", &time-t)

    }
    close(web)
end

procedure showheaders(web)
    every write(left(k:=key(\web), 16), ": ", web[k])
end

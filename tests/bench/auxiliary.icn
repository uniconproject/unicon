############################################################################
#
#	File:     auxiliary.icn
#
#	Subject:  Auxiliary procedures for Unicon benchmark run-* scripts 
#
#	Author:   Shea Newton
#
#	Date:     January 3, 2014
#
############################################################################

link "printf"
link "binary-trees"
link "chameneos-redux"
link "fannkuch"
link "fasta"
link "k-nucleotide"
link "mandelbrot"
link "meteor-contest"
link "n-body"
link "pidigits"
link "regex-dna"
link "reverse-complement"
link "spectral-norm"
link "thread-ring"
link "concord"
link "deal"
link "ipxref"
link "queens"
link "rsg"

procedure CpuToHrMinSec(ms:0)
   s := m := h := 0
   if *ms > 3 then 
      s := ms[1:-3] & ms := ms[-3:0]
   if s > 59 then
      m := s / 60
   if m > 59 then 
      h := m / 60
   return [h, m % 60, s % 60, ms]
end

procedure WallToHrMinSec(s:0)
   m := h := 0
   if s > 59 then
      m := s / 60 
   if m > 59 then 
      h := m / 60
   return [h, m % 60, s % 60]
end

procedure writetime(L1, L2, wallusec)
   if L1[1]>0 then
      printf("%02d:%02d:%d.%03d", L1[1], L1[2], L1[3], L1[4]) 
   else {
      if L2[1]>0 then
	 printf("    %02d:%d.%03d", L1[2], L1[3], L1[4])
      else
	 printf("       %d.%03d", L1[3], L1[4])
      }
   if L1[1]>0 then
      printf("   %02d:%02d:%d.%03d\n", L2[1], L2[2], L2[3], wallusec/1000)
   else {
      if L2[2]>0 then
	 printf("       %02d:%d.%03d\n", L2[2], L2[3], wallusec/1000)
      else
	 printf("          %d.%03d\n", L2[3], wallusec/1000)
      }
end

procedure calctimes(cpustart, cpufinish, wallstart, wallfinish)
   cputime := cpufinish - cpustart
   wallsec := wallfinish.sec - wallstart.sec
   wallusec := wallfinish.usec - wallstart.usec
   if wallusec[1] == "-" then {
      wallsec -:= 1
      wallusec +:= 1000000
      }
   C := CpuToHrMinSec(cputime)
   W := WallToHrMinSec(wallsec)
   return [C, W, wallusec]
end


procedure gettimes(proc, arg)
   wallstart := gettimeofday()
   cpustart := &time
   proc([arg])
   cpufinish := &time
   wallfinish := gettimeofday()
   return [cpustart, cpufinish, wallstart, wallfinish]
end

procedure write_error(msg)

   if msg = 1 then
      write("N/A. Threaded Benchmark.")      
   if msg = 2 then {
      write("N/A. Threaded Benchmark.")
      write("\nConcurrency Not Enabled. \nEnable _
             concurrency to run threaded benchmarks.\n")
      }
   if msg = 3 then {
      write("\nConcurrency not enabled. _
 	     Enable concurrency to run threaded benchmarks.")
      }
end

procedure write_header()
   write()
   write(left("OS",12)," ",left("Word Size",32)," ",left("Main Memory",12))
   write(left(get_OS(),12)," ",left(get_WordSize(),32)," ",
         left(get_Memory(),12))
   write()
   write(left("C Compiler",12)," ",left("CPU",32)," ",left("clock",12))
   write(left(get_C_compiler(),12)," ",left(get_CPU(),32)," ",
         left(get_clockspeed(),12))
   write()
   write("benchmark                     CPU time h:m:s  Real time h:m:s")
   
end

procedure verify_concurrent()
   if &features == "concurrent threads" then
      return 1
end

procedure get_Memory()
   local f, result := "unknown"
   if f := open("/proc/meminfo") then {
      while line := read(f) do {
         if match("MemTotal", line) then {
            line ? {
               tab(upto(&digits))
               result := line[&pos:0]
               }
            break
            }
         }
      close(f)
      }
   return result
end

procedure get_OS()
   return x := &features
end

procedure get_WordSize()
   if match("Arch ", x := &features) then return x[10:0] || " bit"
   else return "unknown"
end

procedure get_C_compiler()
   if match("CCompiler ", x := &features) then return x[11:0]
   else return "unknown"
end
   
procedure get_CPU()
   local f, result := "unknown"
   if &features == "Win32" then {
       result := getenv("PROCESSOR_ARCHITECTURE") # not very good, but its a start
       if map(getenv("PROCESSOR_IDENTIFIER")) ? find("genuineintel") then
	   result := "Intel " || result
       result ||:= ":" || getenv("PROCESSOR_LEVEL") # not very good, but its a start
   }
   else {
       if f := open("/proc/cpuinfo") then {
	   while line := read(f) do {
	       if match("model name", line) then {
		   result := line[find(":",line)+2:0]
		   break
	       }
	   }
	   close(f)
       }
   }
   return result
end
   
procedure get_clockspeed()
   local f, result := "unknown"
   if f := open("/proc/cpuinfo") then {
      while line := read(f) do {
	 if match("cpu MHz", line) then {
	    result := sprintf("%1.1r GHz", line[find(":",line)+2:0] / 1000)
	    break
	    }
	 }
      close(f)
      }
   return result
end

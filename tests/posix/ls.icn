
$include "posix.icn"
link printf

procedure main()
   # (re-)create test-dir so that output is predictable
   system("rm -rf test-dir")
   umask("rwxr-xr-x") | write("set umask fails")
   mkdir("test-dir")
   f := open("test-dir/a","w")
   close(f)
   f := open("test-dir/b","w")
   close(f)
   f := open("test-dir/c","w")
   close(f)

   chdir("test-dir")
   symlink("a", "d")

   f := open(".") | stop(sys_errstr(&errno), image("test-dir"))
   L := list()
   while line := read(f) do
      push(L, line)
   every write(format(stat(n := !sort(L)), n))
end

procedure format(p, name)

   case p.mode[1] of {
       "-": { # Normal file:
               return sprintf("%s (file) permissions %s size %8s", name, p.mode, p.size)
            }
       "d": { # Directory: special case ".."
               if name == ".."
               then return ".. (directory)"
               else return sprintf("%s (directory) permissions %s", name, p.mode)
            }
       "l": { # Symbolic link:
                 return sprintf("%s (symbolic link) -> %s", name, p.symlink)
            }
       default:
           {
               return sprintf("%s Unexpected file type (%s)", name, p.mode[1])
           }
   }

end


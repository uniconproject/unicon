#
# $Id: canvastreedialog.icn,v 1.2 2006-07-09 23:43:13 rparlett Exp $
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
#
#
class CanvasTreeDialog : CanvasComponentDialog(
   c,                       #         
   show_root,
   show_root_handles,
   select_group             #
   )

   method on_okay()
      self.validate_input() | fail
      self.set_vals(c)
      c.select_one := c.select_many := &null
      case select_group.get_selection() of {
         2 :  c.set_select_one()
         3 :  c.set_select_many()
      }
      c.reset(c.parent)
      c.show_root := show_root.get_status()
      c.show_root_handles := show_root_handles.get_status()
      c.tree_structure_changed()
      self.okay_result := 1
      self.dispose()
   end

   initially
      local p
      self.setup(c)
      set_w_default.set_is_shaded()
      set_h_default.set_is_shaded()
      self.set_attribs("size=540,400", "label=" || label)

      p := TabItem()
      add_tab_at_front(p)
      p.set_label("General")

      select_group := List()
      select_group.set_selection_list(["No selection", "Select one", "Select many"])
      select_group.set_size(120)
      select_group.set_pos(250, 25)
      p.add(select_group)

      if \c.select_one then
         select_group.set_selection(2)
      else if \c.select_many then
         select_group.set_selection(3)

      show_root := CheckBox("label=Show root", "pos=50,25")
      if \c.show_root then
         show_root.set_is_checked()
      p.add(show_root)

      show_root_handles := CheckBox("label=Show root handles", "pos=50,65")
      if \c.show_root_handles then
         show_root_handles.set_is_checked()
      p.add(show_root_handles)

end
